/* 
 * Copyright 2016-2021 ISC Konstanz
 * 
 * This file is part of TH-E EMS.
 * For more information visit https://github.com/isc-konstanz/th-e-ems
 * 
 * TH-E EMS is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * TH-E EMS is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public License
 * along with TH-E EMS.  If not, see <http://www.gnu.org/licenses/>.
 *
 */
buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder' version '5.1.2'
}

apply from: 'projects.gradle'

def javaProjects() {
    return allprojects.findAll { new File(it.projectDir, 'src').exists() }
}

configure(javaProjects()) {
    apply plugin: 'java-library'
    apply plugin: 'biz.aQute.bnd.builder'
    apply plugin: 'eclipse'

    group = projectGroup

    version = projectVersion

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation group: 'junit', name: 'junit', version: '4.13.2'

        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.33'
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'
    }

    jar {
        bnd('Bundle-SymbolicName': project.group+'.'+project.name,
            'Bundle-Name':         project.name,
            'Bundle-Vendor':       projectVendor,
            'Bundle-DocURL':       projectRepository)
    }

    if (!project.properties.containsKey('javaVersion')) {
        project.ext {
            javaVersion = '1.8'
        }
    }

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    javadoc {
	    options.encoding = 'utf-8'
        exclude '**/internal/**'
        exclude '**/java-gen/**'
        exclude '**/app/**'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task jarAll(type: Copy) {
        // Include the jar file created, as well as all artifacts:
        from jar
        from javadocJar
        from sourcesJar

        if (copyDependencies) {
            // Includes all the dependencies:
            from configurations.default
        }
        into rootDir.getPath() + '/build/libs'
    }

    build.dependsOn { jarAll }

    eclipse.pathVariables([GRADLE_USER_HOME:file(gradle.gradleUserHomeDir)])
    tasks.eclipse.dependsOn(cleanEclipse)

    artifacts {
        archives javadocJar
        archives sourcesJar
    }
}

configure(rootProject) {
    jar {
        // FIXME: Find better solution to avoid the root project beeing built
        destinationDirectory = new File("$rootDir/build/tmp/libs")
    }
}

task javadocClean(type: Delete) {
    delete rootDir.getPath() + '/docs/javadoc'
}

task javadocAll(type: Javadoc) {
    dependsOn(javadocClean)
    source javadocProjects.collect {
        project -> project.sourceSets.main.allJava
    }
    exclude '**/internal/**'
    exclude '**/java-gen/**'
    exclude '**/app/**'

    destinationDir = new File(rootDir, 'docs/javadoc')

    classpath = files(javadocProjects.collect { project ->
        project.sourceSets.main.compileClasspath })

    classpath += files(javadocProjects.collect { project ->
        project.sourceSets.main.output })
}

task packages {
    apply from: rootDir.getPath() + "/packages/build.gradle"
    dependsOn debianPackages
}

task distribute(type: Tar) {
    dependsOn(distributionProjects.build)
    dependsOn(javadocAll)

    compression = Compression.GZIP
    archiveFileName = project.name + '-' + projectVersion + '.tar.gz'

    into(project.name) {
        from('build') {
            include 'libs/*' + projectVersion + '*.jar'
        }
        from('./') {
            include 'LICENSE**'
            include 'NOTICE**'
            include 'conf/**'
            include 'libs/**'
            include 'licenses/**'
        }
        into('licenses') {
            for (Project distributionProject: distributionProjects) {
                from (distributionProject.getProjectDir().toString() + '/libs') {
                    exclude '**/*.jar'
                    exclude '**.jar'
                }
            }
        }
    }
    destinationDirectory = file('build/dist/')
}
